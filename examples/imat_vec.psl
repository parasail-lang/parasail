
// Example ParaSail modules for int matrix/vector manipulation -- imat_vec.psl

// Copyright (C) 2011-2014, AdaCore, New York, NY
// This program is provided "as is" with no warranty.
// Report errors at http://groups.google.com/group/parasail-programming-language

interface IMatrix<Rows : Univ_Integer; Cols : Univ_Integer> is
  // Define a 2-dimensional (Row x Cols) matrix of Univ_Ints, 

    type Scalar is Univ_Integer;  // The component type

    // The types used for indexing
    type Row is Integer<1..Rows>;
    type Col is Integer<1..Cols>;

    // A "countable" type used to provide sequential iteration
    // over a 2-dimensional matrix; column varies fastest.
    interface Row_Col<> is
        var R : Row;
        var C : Col;
	op "=?"(Left, Right : Row_Col) -> Ordering;
        func Strict_Compare(Left, Right : Row_Col) -> Ordered::Full_Ordering
          is (Left =? Right);
	func First() -> Row_Col;
	func Last() -> Row_Col;
	op "+"(Left : Row_Col; Right : Univ_Integer) -> Row_Col;
	op "+"(Left : Univ_Integer; Right : Row_Col) -> Row_Col;
	op "-"(Left : Row_Col; Right : Univ_Integer) -> Row_Col;
	op "-"(Left : Row_Col; Right : Row_Col) -> Univ_Integer;

	op "+="(var Left : Row_Col; Right : Univ_Integer);
	op "-="(var Left : Row_Col; Right : Univ_Integer);

	op "[..]"() -> Countable_Range<Row_Col> is in Countable_Range<Row_Col>;

	func Hash(Val : Row_Col) -> Unsigned_64;

        func Min(Left, Right : optional Row_Col) -> optional Row_Col;
        func Max(Left, Right : optional Row_Col) -> optional Row_Col;

	op ".."(Left, Right : Row_Col) -> Countable_Set<Row_Col>
	  is in Countable_Set<Row_Col>;
	op "<.."(Left, Right : Row_Col) -> Countable_Set<Row_Col>
	  is in Countable_Set<Row_Col>;
	op "..<"(Left, Right : Row_Col) -> Countable_Set<Row_Col>
	  is in Countable_Set<Row_Col>;
	op "<..<"(Left, Right : Row_Col) -> Countable_Set<Row_Col>
	  is in Countable_Set<Row_Col>;
	op "|"(Left, Right : Row_Col) -> Countable_Set<Row_Col>
	  is in Countable_Set<Row_Col>;

        func To_String(Row_Col) -> Univ_String
          is ("(" | Row_Col.R | ", " | Row_Col.C | ")");
        optional func From_String(Univ : Univ_String)
          -> optional Row_Col is (null)

    end interface Row_Col;

    // Constructors for a matrix.
    func Create(Default : optional Scalar) -> IMatrix;
    op "[]"(Elems : Vector<Scalar>) -> IMatrix;

    // Element-by-element addition/subtraction/negation
    op "+"(Left, Right : IMatrix) -> IMatrix;
    op "-"(Left, Right : IMatrix) -> IMatrix;
    op "+"(Right : IMatrix) -> IMatrix;
    op "-"(Right : IMatrix) -> IMatrix;

    // IMatrix times or divide-by scalar
    op "*"(Left : IMatrix; Right : Scalar) -> IMatrix;
    op "*"(Left : Scalar; Right : IMatrix) -> IMatrix;
    op "/"(Left : IMatrix; Right : Scalar) -> IMatrix;

    // IMatrix multiplication.  This is a "generic" operation
    // where the left and right operands need not have the same
    // numbers of rows and columns.  
    op "*"(Left: IMatrix; Right : Right_Type is IMatrix<>)
      {Cols == Right_Type::Rows}  // Required by rules of matrix multiply
      -> (Result_Type is 
	    IMatrix<Rows => Rows, Cols => Right_Type::Cols>);

    // All-zeros and all ones Constructors
    func Zeroes() -> IMatrix;  // All zeroes
    func Zeros() -> IMatrix is Zeroes;  // A synonym
    func Ones() -> IMatrix;    // All ones

    // Transpose
    func T(M : IMatrix) 
      -> (Result_Type is IMatrix<Rows => Cols, Cols => Rows>);

    // Indexing operations
    op "indexing"(ref M : IMatrix; R : Row; C : Col) -> ref Scalar;
    op "indexing"(ref M : IMatrix; RC : Row_Col) -> ref Scalar;

    // "var" indexing used to implement "[1 => x, 2 => y, 3 => z]" 
    // "named" container aggregate.
    op "var_indexing"(ref var M : IMatrix; R : Row; C : Col) 
      -> ref var Scalar;
    op "var_indexing"(ref var M : IMatrix; RC : Row_Col) 
      -> ref var Scalar;

    // Used for iterating over a matrix
    op "index_set"(M : IMatrix) -> Countable_Set<Row_Col>;

    // Imageable operations
    op "=?"(Left, Right : IMatrix) -> Ordering;
    func Hash(M : IMatrix) -> Unsigned_64;
    func To_String(M : IMatrix) -> Univ_String;
    func From_String(Str : Univ_String) -> IMatrix;
    
    // Used to implement "[x, y, z]" positional container aggregate
    op "[]"() -> IMatrix;
    op "<|="(var M : IMatrix; var Val : optional Univ_Integer);

end interface IMatrix;

interface Col_IVec<Elems : Univ_Integer> 
  extends IMatrix<Rows => Elems, Cols => 1> is
  // Column vector of Elems elements

    // inherited: type Scalar is Univ_Integer;
    type Index is Integer<1..Elems>;

  new  // "new" separates overridings of inherited ops (none in this case)
       // from new operations (below: indexing, Length, and abs in this case).

    // Single dimension indexing
    op "indexing"(ref CV : Col_IVec; I : Index) -> ref Scalar;

    // Number of elements in vector
    func Length(CV : Col_IVec) -> Univ_Integer;

    // Magnitude of vector
    op "abs"(CV : Col_IVec) -> Univ_Real;
    
end interface Col_IVec;

interface Row_IVec<Elems : Univ_Integer> 
  extends IMatrix<Rows => 1, Cols => Elems> is
  // Row vector of length Elems

    // inherited: type Scalar is Univ_Integer;
    type Index is Integer<1..Elems>;

  new

    // Single-dimensional indexing
    op "indexing"(ref RV : Row_IVec; I : Index) -> ref Scalar;

    // Number of elements
    func Length(RV : Row_IVec) -> Univ_Integer;
    
end interface Row_IVec;

interface Square_IMatrix<Elems : Univ_Integer>
  extends IMatrix<Rows => Elems, Cols => Elems> is
  // Square matrix (Elems x Elems)

    // inherited: type Scalar is Univ_Integer;
    type Index is Integer<1..Elems>;

  new

    // Identity matrix
    func Identity() -> Square_IMatrix;   // Identity matrix
    func Ident() -> Square_IMatrix is Identity;   // A synonym for Identity

    // Inverse of matrix.
    // Returns null if not defined.
    func Inverse(SM : Square_IMatrix) -> optional Square_IMatrix;

    // Determinant.
    // Returns null if not defined.
    func Determinant(SM : Square_IMatrix) -> optional Scalar;
    func Det(SM : Square_IMatrix) -> optional Scalar is Determinant;

    // Eigenvalue and eigenvector of matrix.
    // Returns null if not defined.
    func Eigenvalue(SM : Square_IMatrix) -> optional Scalar;
    func Eigenvector(SM : Square_IMatrix) -> optional Col_IVec<Elems>;

end interface Square_IMatrix;

class IMatrix is
  // Define a 2-dimensional (Row x Cols) matrix of Univ_Integer, 
    var Data : Basic_Array<Scalar>;
  exports

    class Row_Col is
      // A "countable" type used to provide sequential iteration
      // over a 2-dimensional matrix; column varies fastest.
      exports
	func First() -> Row_Col is
	    return (1,1);
	end func First;
	func Last() -> Row_Col is
	    return (Rows, Cols);
	end func Last;

	op "=?"(Left, Right : Row_Col) -> Ordering is
	    case Left.R =? Right.R of
	      [#less] =>
		return #less;
	      [#greater] =>
		return #greater;
	      [#equal] =>
		return Left.C =? Right.C;
	    end case;
	end op "=?";

        func Min(Left, Right : optional Row_Col) -> optional Row_Col is
            if Left is null then
                return Right;
            elsif Right is null then
                return Left;
            elsif Left < Right then
                return Left;
            else
                return Right;
            end if;
        end func Min;

        func Max(Left, Right : optional Row_Col) -> optional Row_Col is
            if Left is null then
                return Right;
            elsif Right is null then
                return Left;
            elsif Left > Right then
                return Left;
            else
                return Right;
            end if;
        end func Max;

	op "+"(Left : Row_Col; Right : Univ_Integer) -> Result : Row_Col is
	  // Advance to "Right"th subsequent cell, varying column fastest.
	    if Right < 0 then
		return Left - (-Right);
	    end if;
	    Result := Left;
	    for I in 1..Right loop
		if Result.C == Cols then
		    Result.R += 1;
		    Result.C := 1;
		else
		    Result.C += 1;
		end if;
	    end loop;
	end op "+";

	op "+"(Left : Univ_Integer; Right : Row_Col) -> Row_Col is
	    return Right + Left;
	end op "+";

	op "-"(Left : Row_Col; Right : Univ_Integer) -> Result : Row_Col is
	  // Go back to "Right"th prior cell, varying column fastest.
	    if Right < 0 then
		return Left + (-Right);
	    end if;
	    Result := Left;
	    for I in 1..Right loop
		if Result.C == 1 then
		    Result.R -= 1;
		    Result.C := Cols;
		else
		    Result.C -= 1;
		end if;
	    end loop;
	end op "-";

	op "-"(Left : Row_Col; Right : Row_Col) -> Result : Univ_Integer is
	  // Number of cells separating two locations in matrix
	    Result := 0;
	    case Left =? Right of
	      [#less] =>
		var RC : Row_Col := Left;
		while RC < Right loop
		    Result -= 1;
		    RC := RC + 1;
		end loop;
		return Result;
	      [#greater] =>
		var RC : Row_Col := Left;
		while RC > Right loop
		    Result += 1;
		    RC := RC - 1;
		end loop;
		return Result;
	      [#equal] =>
		return 0;
	    end case;
	end op "-";

	op "+="(var Left : Row_Col; Right : Univ_Integer) is
           Left := Left + Right;
        end op "+=";

	op "-="(var Left : Row_Col; Right : Univ_Integer) is
           Left := Left - Right;
        end op "-=";

	func Hash(Val : Row_Col) -> Unsigned_64 is
	    return [[Val.R-1]] * Cols + [[Val.C]];
	end func Hash;

    end class Row_Col;

    func Create(Default : optional Scalar) -> IMatrix is
      // Create a Rows x Cols matrix, all initialized to "Default"
	return (Data => Create(Rows * Cols, Default));
    end func Create;

    op "[]"(Elems : Vector<Scalar>) -> Result : IMatrix is
      // Create a IMatrix given an appropriately sized vector
	{Length(Elems) == Rows * Cols}
	Result := Create(0);
	for each [RC => E] of Result concurrent loop
	    E := Elems[ [[RC.R-1]] * Cols + [[RC.C]] ];
	end loop;
    end op "[]";

    op "+"(Left, Right : IMatrix) -> Result : IMatrix is
      // Element-wise addition
	Result := Create(0);
	for each [RC => E] of Result concurrent loop
	    E := Left[RC] + Right[RC];
	end loop;
    end op "+";

    op "-"(Left, Right : IMatrix) -> Result : IMatrix is
      // Element-wise subtraction
	Result := Create(0);
	for each [RC => E] of Result concurrent loop
	    E := Left[RC] - Right[RC];
	end loop;
    end op "-";

    op "+"(Right : IMatrix) -> IMatrix is
	return Right;
    end op "+";

    op "-"(Right : IMatrix) -> IMatrix is
	return -1 * Right;
    end op "-";

    op "*"(Left : IMatrix; Right : Scalar) -> Result : IMatrix is
      // Scalar multiplication
	Result := Left;
	for each [RC => E] of Result concurrent loop
	    E *= Right;
	end loop;
    end op "*";

    op "*"(Left : Scalar; Right : IMatrix) -> IMatrix is
	// Just pass the buck to the other one
	return Right * Left;
    end op "*";

    op "/"(Left : IMatrix; Right : Scalar) -> Result : IMatrix is
      // Scalar division
	Result := Left;
	for each [RC => E] of Result concurrent loop
	    E /= Right;
	end loop;
    end op "/";
        
    op "*"(Left: IMatrix; Right : Right_Type is IMatrix<>)
      {Cols == Right_Type::Rows}
      -> (Result : Result_Type is 
	    IMatrix<Rows => Rows, Cols => Right_Type::Cols>) is
      // IMatrix multiplication

	{Cols == Right_Type::Rows}
	{Rows == Result_Type::Rows}
	{Result_Type::Cols == Right_Type::Cols}

	Result := Result_Type::Create(0);
	for I in 1..Result_Type::Rows concurrent loop
	    for J in 1..Result_Type::Cols concurrent loop
		var Mac : Scalar := 0;
		for K in Col loop
		    Mac += Left[I, [[K]]] * Right[ [[K]], J];
		end loop;
		Result[I, J] := Mac;
	    end loop;
	end loop;

    end op "*";

    func Zeroes() -> IMatrix is // All zeroes
	return Create(0);
    end func Zeroes;

    func Ones() -> IMatrix is // All ones
	return Create(1);
    end func Ones;

    func T(M : IMatrix) 
      -> (Result : Result_Type is IMatrix<Rows => Cols, Cols => Rows>) is
      // Transpose
	{ Result_Type::Rows == Cols; Result_Type::Cols == Rows }
	Result := Result_Type::Create(0);
	for I in 1..Rows concurrent loop
	    for J in 1..Cols concurrent loop
		Result[J, I] := M[I, J];
	    end loop;
	end loop;

	// for each [RC => E] of Result concurrent loop  // TBD: Should work
	    // E := M[ [[RC.C]], [[RC.R]] ];
	// end loop;
    end func T;

    op "indexing"(ref M : IMatrix; R : Row; C : Col) -> ref Scalar is
	const Index := [[R-1]] * Cols + [[C]];
	return M.Data[ Index ];
    end op "indexing";

    op "indexing"(ref M : IMatrix; RC : Row_Col) -> ref Scalar is
	return M[RC.R, RC.C];
    end op "indexing";

    op "var_indexing"(ref var M : IMatrix; R : Row; C : Col) 
      -> ref var Scalar is
	return M.Data[ [[R-1]] * Cols + [[C]] ];
    end op "var_indexing";

    op "var_indexing"(ref var M : IMatrix; RC : Row_Col) 
      -> ref var Scalar is
	return M[RC.R, RC.C];
    end op "var_indexing";

    op "index_set"(M : IMatrix) -> Countable_Set<Row_Col> is
	return (1,1) .. (Rows, Cols);
    end op "index_set";
    
    op "=?"(Left, Right : IMatrix) -> Ordering is
        for I in 1..Rows loop
            for J in 1..Cols loop
                if Left[I, J] != Right[I, J] then
                    return #unordered;
                end if;
            end loop;
        end loop;
        return #equal;
    end op "=?"

    func Hash(M : IMatrix) -> Unsigned_64 is
        return Unsigned_64::(M[1,1]) + Unsigned_64::(M[Rows, Cols]);
    end func Hash;

    func To_String(M : IMatrix) -> Univ_String is
	// Convert matrix into string with line breaks
	var Images : Array<Univ_String, Indexed_By => Row_Col> := 
	  Create([..], "");

	var Max_Len := 0;
	for I in 1..Rows loop
	    for J in 1..Cols loop
		const Image : Univ_String := To_String(M[I, J]);
		Images[(I, J)] := Image;
		Max_Len := Max(Max_Len, Length(Image));
	    end loop;
	end loop;

        // Build up a character vector, then convert to a string
        type Char_Vec is Vector<Univ_Character>
	var Result : Char_Vec := ['\n'];

	for I in 1..Rows forward loop
	    for J in 1..Cols forward loop
		const Image : Char_Vec := To_Vector(Images[(I, J)]);
		const Pad : Char_Vec :=
                  Create(Max_Len - Length(Image) + 1, ' ');
		
		Result |= Pad;
                Result |= Image;
	    end loop;
	    Result |= '\n';
	end loop;

        // Convert from character vector to string
        return From_Vector(Result);
	
    end func To_String;

    func From_String(Str : Univ_String) -> Result : IMatrix is
	Result := Create(0);
	// Rest is TBD
    end func From_String;
	
    op "[]"() -> IMatrix is
	// Create a matrix of nulls
	return Create(null);
    end op "[]";

    op "<|="(var M : IMatrix; var Val : optional Univ_Integer) is
	// Fill in next null element
	for each E of M forward loop
	    if E is null then
		E <== Val;
		return;
	    end if;
	end loop;
    end op "<|=";

end class IMatrix;

class Col_IVec is
  // A column vector, of length Elems
  exports

    op "indexing"(ref CV : Col_IVec; I : Index) -> ref Scalar is
	return CV[ [[I]], 1];
    end op "indexing";

    func Length(CV : Col_IVec) -> Univ_Integer is
	return Elems;
    end func Length;

    op "abs"(CV : Col_IVec) -> Univ_Real is
	const Prod := T(CV) * CV;
	return Sqrt(Int_To_Real(Prod[1,1])); 
    end op "abs";

end class Col_IVec;

class Row_IVec is
  // A row vector of length Elems
  exports

    op "indexing"(ref RV : Row_IVec; I : Index) -> ref Scalar is
	return RV[ 1, [[I]] ];
    end op "indexing";

    func Length(RV : Row_IVec) -> Univ_Integer is
	return Elems;
    end func Length;

end class Row_IVec;

class Square_IMatrix is
  // An Elems x Elems square matrix
  exports

    func Identity() -> Result : Square_IMatrix is // Identity matrix
	Result := Create(+0);
	for I in 1..Elems loop
	    Result[I, I] := 1;
	end loop;
	return Result;
    end func Identity;
	
    func Inverse(SM : Square_IMatrix) -> optional Square_IMatrix is
	return SM; // TBD
    end func Inverse;

    func Determinant(SM : Square_IMatrix) -> optional Scalar is
	return SM[1, 1]; // TBD
    end func Determinant;

    func Eigenvalue(SM : Square_IMatrix) -> Result : optional Scalar is
	Result := SM[1, 1];  // TBD
    end func Eigenvalue;

    func Eigenvector(SM : Square_IMatrix) -> Result : optional Col_IVec<Elems> is
	Result := Create(1);  // TBD
    end func Eigenvector;

end class Square_IMatrix;

func Test_IMat_Vec() is
  // Test some of the matrix operations
    type Two_By_Two is Square_IMatrix<2>;
    type Two_By_Three is IMatrix<2, 3>;
    var I : Two_By_Two := Ident();
    var Y := I * 2;
    var One : Two_By_Two := Ones();

    Println("Ident =");
    Println(To_String(Two_By_Two::Ident()));

    Println("Ones = ");
    Println(To_String(One));

    Println("IMatrix Y = 2 * Ident():\n" | To_String(Y));
    Println("Ones() + Y =\n" | To_String(Ones() + Y));

    Y[1, 2] += 5;
    Println("Y[1,2] += 5; Y:\n" | To_String(Y));

    const M : Two_By_Three := [11, 22, 33, 44, 55, 66];

    Println("IMatrix M = [11, 22, 33, 44, 55, 66] = ");
    Println(To_String(M));

    Println("M*2 =" | M * 2);

    Println("M/2 =" | M / 2);

    Println("T(Y):" | T(Y));

    Println("Y * T(Y):" | Y * T(Y));

    for each [RC => E] of Y loop
	Println("R: " | RC.R | ", C: " | RC.C | ", val = " | E);
    end loop;

    const MT := T(M); 

    Println("M:" | M | "\nT(M):" | T(M));

    Println("M * T(M):" | M * T(M));

    var V : Col_IVec<4> := [11, 33, 55, 77];
    V[1] += 1;
    Println("V:" | V);

    for J in 1..Length(V) forward loop
	Println("V[" | J | "] = " | V[J]);
    end loop;

    const TV := T(V);
    Println("T(V):" | TV);

    const RV : Row_IVec<4> := [22, 44, 66, 88];
    Println("RV:" | RV);

    const TRV := T(RV);
    Println("TRV:" | TRV);

    const TTV : Col_IVec<4> := T(TV);
    Println("TTV:" | TTV);

    const TRVC : Col_IVec<4> := T(RV);
    Println("TRVC:" | TRVC);

    const Mag := abs(TRVC);
    Println("abs(TRVC) = " | Mag);

    type CV4 is Col_IVec<4>;

    // const Mag2 := abs(CV4::T(RV));   // Should work
    // Println("abs(CV4::T(RV)) = " | Mag2);

    const Mag3 := CV4::"abs"(T(RV));

    const SM2 : Square_IMatrix<2> := [11, 33, 22, 44];
    Println("\nSM2: " | SM2);

    const TSM2 : Square_IMatrix<2> := T(SM2);
    Println("TSM2: " | TSM2);

end func Test_IMat_Vec;

